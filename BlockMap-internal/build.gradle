plugins {
    id 'com.jaredsburrows.license'
    id 'org.openjfx.javafxplugin' version '0.0.10'
	id "me.champeau.gradle.jmh" version "0.5.3"
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.swing' ]
}

task generateData(type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	main = "de.piegames.blockmap.generate.Generator"
	args = ['generateData']
}

task generateSources(type: JavaExec) {
	description 'Generate all enum values from BlockState.java and the version sources'
	classpath = sourceSets.main.runtimeClasspath
	main = "de.piegames.blockmap.generate.Generator"
	args = ["generateVersion;${project.version}"]
}

task generateScreenshots(type: JavaExec) {
	group 'Generation'
	description 'Automatically render certain files and create screenshots from the GUI to show off on Twitter. Takes a really long time to process because a large demo world is generated first.'
	classpath = sourceSets.main.runtimeClasspath
	main = "de.piegames.blockmap.generate.Generator"
	args = ['generateTestWorld', 'generateScreenshots']
}

task checkMinecraftVersions(type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	main = "de.piegames.blockmap.generate.Generator"
	args = ['checkMinecraftVersions']
}

dependencies {
	testImplementation 'junit:junit:4.13.2'

	implementation project(':BlockMap-core')
	implementation project(':BlockMap-gui')
	implementation project(':BlockMap-cli')
	implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
	implementation 'commons-io:commons-io:2.7'
	implementation 'com.google.guava:guava:31.0.1-jre'
	implementation 'info.picocli:picocli:4.5.0'
	implementation 'com.github.Minecraft-Technik-Wiki:MinecraftLandGenerator:3.1.2'
	implementation 'org.controlsfx:controlsfx:11.1.1'
	implementation group: 'com.codepoetics', name: 'protonpack', version: '1.16'
	implementation 'com.vdurmont:semver4j:3.1.0'

	/* This is not an actual dependency, but some biome color data has been copied from there so it will show up in the credits list */
	//implementation 'com.github.toolbox4minecraft:amidst:v4.3'
	
    jmh group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.34'
    jmh group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.34'
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
		resources {
			srcDirs = ['src/main/resources', "./build/generated-resources/${project.name}/generated-resources-main"]
		}
	}
	jmh {
		java {
			srcDirs = ['src/jmh/java']
		}
		resources {
			srcDirs = ['src/jmh/resources']
		}
	}
	test {
		java {
			srcDirs = ['src/test/java']
		}
		resources {
			srcDirs = ['src/test/resources', "./build/generated-resources/${project.name}/generated-resources-test"]
		}
	}
}

test {
	exclude 'de/piegames/blockmap/AllTests.class'

	testLogging {
		exceptionFormat = 'full'
	}
}

jmh {
   // Hacky workaround for https://github.com/melix/jmh-gradle-plugin/issues/122
   jvmArgsAppend = ["-Dbenchmark.resources=" + "${sourceSets.main.resources.srcDirs[0].toString()}"]
}
